apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'eu.thog92'
archivesBaseName = 'lwjall'
version = appVersion


configurations {natives}

def platforms = ['windows', 'linux', 'osx']

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

if (System.env.BUILD_NUMBER != null)  version = "$version.$System.env.BUILD_NUMBER"

project.ext.lwjglVersion = "3.0.0b"

dependencies {
    compile 'org.jcraft:jorbis:0.0.17'
    compile 'org.joml:joml:1.6.5'
    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-windows"
    compile "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-linux"
    compile "org.lwjgl:lwjgl-platform:${lwjglVersion}:natives-osx"
}

// JAR manifest configuration
jar.manifest.mainAttributes(
    "Built-By": System.properties['user.name'],
    "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")")

platforms.each {platform ->
    task "${platform}Natives" {
        def outputDir = "$buildDir/natives/$platform"
        inputs.files(configurations.natives)
        outputs.dir(outputDir)
        doLast {
            copy {
                def artifacts = configurations.natives.resolvedConfiguration.resolvedArtifacts
                    .findAll {it.classifier == "natives-$platform"}
                artifacts.each {
                    from zipTree(it.file)
                }
                into outputDir
            }
        }
    }
}

task natives {
    description "Copies native libraries to an appropriate directory."
    dependsOn platforms.collect {"${it}Natives"}.findAll {tasks[it]}
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

artifacts {
    archives sourcesJar
}
